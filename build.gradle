plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.0'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'org.hibernate.orm' version '6.2.9.Final'
}

group = 'fr.da-sha1'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '20'
}

repositories {
	mavenCentral()
}

ext {
	libraries = [
			swagger : [
				dependencies.create("io.swagger.core.v3:swagger-core-jakarta:$swaggerVersion"),
				dependencies.create( "io.swagger.core.v3:swagger-models:$swaggerVersion")
			],
			jpa : [
				dependencies.create("com.zaxxer:HikariCP"),
				dependencies.create("org.hibernate.orm:hibernate-core"),
				dependencies.create("org.springframework.data:spring-data-jpa")
			],
			web : [
				dependencies.create("org.springframework:spring-webmvc"),
				dependencies.create("org.apache.tomcat.embed:tomcat-embed-core"),
				dependencies.create("com.fasterxml.jackson.module:jackson-module-parameter-names"),
				dependencies.create("com.fasterxml.jackson.datatype:jackson-datatype-jdk8"),
				dependencies.create("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
			],
			boot : [
				dependencies.create("org.springframework.boot:spring-boot-autoconfigure"),
				dependencies.create("jakarta.annotation:jakarta.annotation-api")
			],
			logging : [
				dependencies.create("ch.qos.logback:logback-classic")
			]
	]
}

dependencies {
	implementation libraries.jpa
	implementation libraries.boot
	implementation libraries.web
	implementation libraries.logging

	//implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation "org.mapstruct:mapstruct:$mapstructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

	implementation libraries.swagger

	//dependencies excluded from an executable jar or war
	// swagger dependencies are only used for development
	developmentOnly "org.springdoc:springdoc-openapi-starter-webmvc-ui:$springdocOpenapiVersion"
	// actuators endpoints are only used for development
	developmentOnly 'org.springframework.boot:spring-boot-starter-actuator'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	runtimeOnly 'com.h2database:h2:2.2.224'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named("bootBuildImage") {
    imageName = "${project.name}:reduce"
}

tasks.named('test') {
	useJUnitPlatform()
}
