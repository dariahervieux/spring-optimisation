plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.0'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'org.hibernate.orm' version '6.2.9.Final'
	id 'org.graalvm.buildtools.native' version '0.9.27'
}

group = 'fr.da-sha1'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '20'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation "org.mapstruct:mapstruct:$mapstructVersion"

	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$springdocOpenapiVersion"
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	runtimeOnly 'com.h2database:h2:2.2.224'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named("bootBuildImage") {
    imageName = "${project.name}:aot"

    builder="paketobuildpacks/builder:tiny"
    environment= [
            "BP_JVM_VERSION": "21.0.1",
            "BP_NATIVE_IMAGE": "true",
        ]
    buildpacks= [
            "gcr.io/paketo-buildpacks/graalvm",
            "gcr.io/paketo-buildpacks/java-native-image",
        ]

}


// setting spring profile for AOT build
//https://docs.spring.io/spring-boot/docs/current/reference/html/howto.html#howto.aot.conditions
tasks.withType(org.springframework.boot.gradle.tasks.aot.ProcessAot).configureEach {
	args('--spring.profiles.active=default,prod')
}

tasks.named('test') {
	useJUnitPlatform()
}


